# -*-awk-*-
BEGIN {
    header()
    reset()
}

match($0, /__BEGIN_FILE__ (.*)___(k([0-9]+)(.*seed([0-9]+)(.*np([0-9]+))?)?)?/, m) {
    data["Graph"] = m[1]
    if (3 in m) data["K"] = m[3]
    if (5 in m) data["Seed"] = m[5]
    if (7 in m) data["NumProcessors"] = m[7]
}

/__END_FILE__/ {
    yield()
}

match($0, /n:([0-9]+) m: ([0-9]+)/, m) {
    data["N"] = m[1]
    data["M"] = m[2]
}

match($0, /log>final edge cut ([0-9]+)/, m) {
    data["Cut"] = m[1]
}

match($0, /log>final balance ([0-9\.e\-]+)/, m) {
    data["Balance"] = m[1] - 1
}

match($0, /log>total partitioning time elapsed ([0-9\.e\-]+)/, m) {
    data["Time"] = m[1]
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Seed,"
    printf "Cut,"
    printf "Balance,"
    printf "Time,"
    printf "NumProcessors,"
    printf "Failed\n"
}

function yield() {
    if (length(data) <= 4) { return }
    
    printf "%s,", data["Graph"]
    printf "%d,", data["N"]
    printf "%d,", data["M"]
    printf "%d,", data["K"]
    printf "%d,", data["Seed"]
    printf "%d,", data["Cut"]
    printf "%f,", data["Balance"]
    printf "%f,", data["Time"]
    printf "%d,", data["NumProcessors"]
    printf "%d\n", length(data) != 9
    
    reset()
}

function reset() {
    split("", data)
    data["NumProcessors"] = -1
    data["Seed"] = -1
    data["K"] = -1
    data["Graph"] = "null"
}
