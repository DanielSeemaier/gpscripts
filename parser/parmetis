# -*-awk-*-
BEGIN {
    header()
    reset()
}

match($0, /__BEGIN_FILE__ (.*)___(.*np([0-9]+)(.*seed([0-9]+))?)?/, m) {
    data["Graph"] = m[1]
    if (3 in m) data["NumProcessors"] = m[3]
    if (5 in m) data["Seed"] = m[5]
}

/__END_FILE__/ {
    yield()
}

match($0, /Final\s*([0-9]+)-way Cut:\s*([0-9]+)\s*Balance:\s*([0-9e\-\.]+)/, m) {
    data["K"] = m[1]
    data["Cut"] = m[2]
    data["Balance"] = m[3]
}

match($0, /Total:\s*Max:\s*([0-9e\-\.]+)/, m) {
    data["Time"] = m[1]
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Seed,"
    printf "Cut,"
    printf "Balance,"
    printf "Time,"
    printf "NumProcessors,"
    printf "Failed\n"
}

function yield() {
    if (length(data) <= 5) { return }
    
    printf "%s,", data["Graph"]
    printf "%d,", data["N"]
    printf "%d,", data["M"]
    printf "%d,", data["K"]
    printf "%d,", data["Seed"]
    printf "%d,", data["Cut"]
    printf "%f,", data["Balance"]
    printf "%f,", data["Time"]
    printf "%d,", data["NumProcessors"]
    printf "%d\n", length(data) != 9
    
    reset()
}

function reset() {
    split("", data)
    data["N"] = -1
    data["M"] = -1
    data["NumProcessors"] = -1
    data["Seed"] = -1
    data["Graph"] = "null"
}
