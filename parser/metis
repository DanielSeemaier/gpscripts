# -*-awk-*-
BEGIN {
    header()
    reset()
}

/__END_FILE__/ {
    yield()
}

match($0, /Name: .*\/(.*\.graph), #Vertices: ([0-9]+), #Edges: ([0-9]+), #Parts: ([0-9]+)/, m) {
    data["Graph"] = m[1]
    data["N"] = m[2]
    data["M"] = m[3] * 2
    data["K"] = m[4]
}

match($0, /ufactor=([0-9\.]+)/, m) {
    data["Epsilon"] = m[1] - 1.0
}

match($0, /seed=(\-?[0-9]+)/, m) {
    data["Seed"] = m[1]
}

match($0, /Edgecut: ([0-9]+)/, m) {
    data["Cut"] = m[1]
}

match($0, /constraint #0:\s+([0-9\.]+)/, m) {
    data["Balance"] = m[1] - 1
}

match($0, /Partitioning:\s+([0-9\.]+)/, m) {
    data["Time"] = m[1]
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Epsilon,"
    printf "Seed,"
    printf "Cut,"
    printf "Balance,"
    printf "Time,"
    printf "Failed\n"
}

function yield() {
    if (length(data) == 0) { return }
    
    printf "%s,", data["Graph"]
    printf "%d,", data["N"]
    printf "%d,", data["M"]
    printf "%d,", data["K"]
    printf "%f,", data["Epsilon"]
    printf "%d,", data["Seed"]
    printf "%d,", data["Cut"]
    printf "%f,", data["Balance"]
    printf "%f,", data["Time"]
    printf "%d\n", length(data) != 9
    
    
    reset()
}

function reset() {
    split("", data)
}
