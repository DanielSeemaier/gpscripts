# -*-awk-*-
BEGIN {
    header()
    reset()
}

match($0, /__BEGIN_FILE__ (.*)___(k([0-9]+)(.*seed([0-9]+))?)?/, m) {
    data["Graph"] = m[1]
    if (3 in m) data["K"] = m[3]
    if (5 in m) data["Seed"] = m[5]
}

/__END_FILE__/ {
    yield()
}

match($0, /graph has ([0-9]+) nodes and ([0-9]+) edges/, m) {
    data["N"] = m[1]
    data["M"] = m[2]
}

match($0, /cut\s*([0-9]+)/, m) {
    data["Cut"] = m[1]
}

match($0, /balance\s*([0-9\.e\-]+)/, m) {
    data["Balance"] = m[1] - 1
}

match($0, /time spent for partitioning ([0-9\.e\-]+)/, m) {
    data["Time"] = m[1]
}

END {
    yield()
}

function header() {
    printf "Graph,"
    printf "N,"
    printf "M,"
    printf "K,"
    printf "Seed,"
    printf "Cut,"
    printf "Balance,"
    printf "Time,"
    printf "Failed\n"
}

function yield() {
    if (length(data) <= 3) { return }
    
    printf "%s,", data["Graph"]
    printf "%d,", data["N"]
    printf "%d,", data["M"]
    printf "%d,", data["K"]
    printf "%d,", data["Seed"]
    printf "%d,", data["Cut"]
    printf "%f,", data["Balance"]
    printf "%f,", data["Time"]
    printf "%d\n", length(data) != 8
    
    reset()
}

function reset() {
    split("", data)
    data["Seed"] = -1
    data["K"] = -1
    data["Graph"] = "null"
}
